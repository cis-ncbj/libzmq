# ZeroMQ cmake module
#
# The following import targets are created
#
# ::
#
#   libzmq-static
#   libzmq
#
# This module sets the following variables in your project::
#
#   ZeroMQ_FOUND - true if ZeroMQ found on the system
#   ZeroMQ_INCLUDE_DIR - the directory containing ZeroMQ headers
#   ZeroMQ_LIBRARY - 
#   ZeroMQ_STATIC_LIBRARY

@PACKAGE_INIT@

if(NOT TARGET ZeroMQ::libzmq AND NOT TARGET ZeroMQ::libzmq-static)
  # Handle dependencies
  include(CMakeFindDependencyMacro)

  find_dependency(Threads)

  set(DEPEND_LIBSODIUM @WITH_LIBSODIUM@)
  if(DEPEND_LIBSODIUM)
      # For some reason find_dependency does not work in child projects (cmake 3.13)
      find_package(libsodium CONFIG)
  endif()

  include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")

  if (TARGET ZeroMQ::libzmq)
    get_target_property(@PROJECT_NAME@_INCLUDE_DIR ZeroMQ::libzmq INTERFACE_INCLUDE_DIRECTORIES)
    get_target_property(@PROJECT_NAME@_LIBRARY ZeroMQ::libzmq LOCATION)
  endif()
  if (TARGET ZeroMQ::libzmq-static)
    get_target_property(@PROJECT_NAME@_STATIC_LIBRARY ZeroMQ::libzmq-static LOCATION)
  endif()
endif()
